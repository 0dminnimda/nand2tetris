// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/02/ALU.hdl

/**
 * The ALU (Arithmetic Logic Unit).
 * Computes one of the following functions:
 * x+y, x-y, y-x, 0, 1, -1, x, y, -x, -y, !x, !y,
 * x+1, y+1, x-1, y-1, x&y, x|y on two 16-bit inputs, 
 * according to 6 input bits denoted zx,nx,zy,ny,f,no.
 * In addition, the ALU computes two 1-bit outputs:
 * if the ALU output == 0, zr is set to 1; otherwise zr is set to 0;
 * if the ALU output < 0, ng is set to 1; otherwise ng is set to 0.
 */

// Implementation: the ALU logic manipulates the x and y inputs
// and operates on the resulting values, as follows:
// if (zx == 1) set x = 0        // 16-bit constant
// if (nx == 1) set x = !x       // bitwise not
// if (zy == 1) set y = 0        // 16-bit constant
// if (ny == 1) set y = !y       // bitwise not
// if (f == 1)  set out = x + y  // integer 2's complement addition
// if (f == 0)  set out = x & y  // bitwise and
// if (no == 1) set out = !out   // bitwise not
// if (out == 0) set zr = 1
// if (out < 0) set ng = 1

CHIP ALU {
    IN  
        x[16], y[16],  // 16-bit inputs        
        zx, // zero the x input?
        nx, // negate the x input?
        zy, // zero the y input?
        ny, // negate the y input?
        f,  // compute out = x + y (if 1) or x & y (if 0)
        no; // negate the out output?

    OUT 
        out[16], // 16-bit output
        zr, // 1 if (out == 0), 0 otherwise
        ng; // 1 if (out < 0),  0 otherwise

    PARTS:
    // zx
    Mux16(a=x, b=false, sel=zx, out=x-z);
    // Not(in=zx, out=not-zx);
    // And16(a=not-zx, b=x, out=x);

    // nx
    Not16(in=x-z,   out=neg-x);
    Mux16(a=x-z, b=neg-x, sel=nx, out=x-zn);

    // zy
    Mux16(a=y, b=false, sel=zy, out=y-z);
    // Not(in=zy, out=not-zy);
    // And16(a=not-zy, b=y, out=y);

    // ny
    Not16(in=y-z,   out=neg-y);
    Mux16(a=y-z, b=neg-y, sel=ny, out=y-zn);

    // f
    Add16(a=x-zn, b=y-zn, out=sum);
    And16(a=x-zn, b=y-zn, out=and);
    Mux16(a=and, b=sum, sel=f, out=res);

    // no
    Not16(in=res,     out=neg-res);
    Mux16(a=res, b=neg-res, sel=no, out=res-n,
          out[0..7]=res-n-1, out[8..15]=res-n-2);

    // zr
    Or8Way(in=res-n-1,  out=or-1);
    Or8Way(in=res-n-2, out=or-2);
    Or(a=or-1, b=or-2, out=all-or);
    Not(in=all-or, out=zr);

    // ng
    Not16(in=res-n, out=neg-res-n);
    Not16(in=neg-res-n, out=out, out[15]=ng);
}